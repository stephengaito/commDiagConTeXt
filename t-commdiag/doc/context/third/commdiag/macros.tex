% A ConTeXt document [master document: commdiag.tex]

\startchapter[title=Macros]

\startMkIVCode

\defineMPinstance
  [commDiag]
  [
    format=metafun,
    extensions=yes,
    initializations=yes,
    method=double
  ]
  
\startMPdefinitions{commDiag}
  def setupCommDiags = 
    picture objLabel[][];
    pair w[][];
    numeric numRows; numRows := 0;
    numeric numCols; numCols := 0;
  enddef ;

  def updateRowsCols(expr i, j) =
    if numRows < i : numRows := i; fi ;
    if numCols < j : numCols := j; fi ;
  enddef;
  
  def addObject(expr row, col, aLabel) =
    updateRowsCols(row, col);
    objLabel[row][col] := thelabel("$"&aLabel&"$", origin);
  enddef ;

  def drawObjects(expr rowWidth, colWidth) = 
    for i = 1 upto numRows : 
      for j = 1 upto numCols :
        w[i][j] = (j*colWidth*cm, (-i)*rowWidth*cm);
        objLabel[i][j] := objLabel[i][j] shifted w[i][j];
        draw objLabel[i][j];
      endfor;
    endfor;
  enddef;
  
  % Based on the definition of "arrowhead" 
  % in file tex/texmf/metapost/base/plain.mp 
  % in the ConTeXt experimental distribution.
  % Taken on 10 August 2018.
  % The file's header is:
  %
  %% % $Id: plain.mp,v 1.3 2005/04/28 06:45:21 taco Exp $
  %% % Public domain.
  %

  newinternal sahFraction;
  sahFraction := 0.9; % default second arrowhead point 90% of arrow path
  
  %% Forward arrow heads/tails ...
  
  vardef firstArrowHead expr p =
    save q,e; path q; pair e;
    e := point length p of p;
    q := gobble(p shifted -e cutafter makepath(pencircle scaled 2ahlength))
      cuttings;
    (q rotated .5ahangle & reverse q rotated -.5ahangle -- cycle)  shifted e
  enddef;
  
  vardef secondArrowHead expr p =
    save q, e; path q; pair e;
    e := point (sahFraction*length p) of p;
    q := gobble(
      subpath (0, (sahFraction*length p)) of p shifted -e 
      cutafter makepath(pencircle scaled 2ahlength)
    ) cuttings;
    (q rotated .5ahangle & reverse q rotated -.5ahangle -- cycle)  shifted e
  enddef;
  
  vardef firstArrowTail expr p =
    save q,eA, eB; path q; pair eA, eB;
    eA := point 0 of p;
    q := gobble(p shifted -eA cutbefore makepath(pencircle scaled 2ahlength))
      cuttings;
    eB := point length(q) of q;
    q := q shifted -eB ;
    (q rotated .5ahangle & reverse q rotated -.5ahangle -- cycle)
      shifted (eA + eB)
  enddef;
  
  vardef secondArrowTail expr p =
    save q,eA, eB, aFraction; path q; pair eA, eB; numeric aFraction;
    aFraction := 2*(1.0 - sahFraction);
    eA := point (aFraction * length p) of p;
    q := gobble(
      subpath (0, (aFraction * length p)) of p shifted -eA
      cutafter makepath(pencircle scaled 2ahlength)
    ) cuttings;
    eB := point length(q) of q;
    q := q shifted -eB ;
    (q rotated .5ahangle & reverse q rotated -.5ahangle -- cycle)
      shifted (eA + eB)
  enddef;

  %% Reversed arrow heads/tails ...
  
  vardef firstReversedArrowHead expr p =
    save q, eA, eB; path q; pair eA, eB;
    eA := point length p of p;
    q := gobble(
      p shifted -eA 
      cutafter makepath(pencircle scaled 2ahlength)
    ) cuttings;
    eB := point 0 of q ;
    q := q shifted -eB ;
    ( reverse q rotated .5ahangle & q rotated -.5ahangle -- cycle)
      shifted (eA + eB)
  enddef;

  vardef secondReversedArrowHead expr p =
    save q, eA, eB; path q; pair eA, eB;
    eA := point (sahFraction*length p) of p;
    q := gobble(
      subpath (0, (sahFraction*length p)) of p shifted -eA 
      cutafter makepath(pencircle scaled 2ahlength)
    ) cuttings;
    eB := point 0 of q ;
    q := q shifted -eB ;
    (reverse q rotated .5ahangle & q rotated -.5ahangle -- cycle)
      shifted (eA + eB)
  enddef;

  vardef firstReversedArrowTail expr p =
    save q,eA, eB; path q; pair eA, eB;
    eA := point 0 of p;
    q := gobble(
      p shifted -eA
      cutbefore makepath(pencircle scaled 2ahlength)
    ) cuttings;
    (reverse q rotated .5ahangle & q rotated -.5ahangle -- cycle)
      shifted eA
  enddef;
  
  vardef secondReversedArrowTail expr p =
    save q,eA, eB, aFraction; path q; pair eA, eB; numeric aFraction;
    aFraction := 2*(1.0 - sahFraction);
    eA := point (aFraction * length p) of p;
    q := gobble(
      subpath (0, (aFraction * length p)) of p shifted -eA
      cutafter makepath(pencircle scaled 2ahlength)
    ) cuttings;
    eB := point 0 of q;
    q := q shifted -eB ;
    ( reverse q rotated .5ahangle & q rotated -.5ahangle -- cycle)
      shifted (eA + eB)
  enddef;
  
  %% Dot heads/tails ...
  
  vardef dotHead expr p =
    save q,eA,eB; path q; pair eA, eB;
    eA := point length p of p;
    eB := p shifted -eA intersectionpoint makepath(pencircle scaled ahlength) ;
    makepath(pencircle scaled ahlength) shifted (eA+eB)
  enddef;
  
  vardef dotTail expr p =
    save q,eA,eB; path q; pair eA, eB;
    eA := point 0 of p;
    eB := p shifted -eA intersectionpoint makepath(pencircle scaled ahlength) ;
    makepath(pencircle scaled ahlength) shifted (eA+eB)
  enddef;

  %% Bar heads/tails ...
  
  vardef barHead expr p =
    save q,e; path q; pair e;
    e := point length p of p;
    q := gobble(
      p shifted -e scaled 100
      cutafter makepath(pencircle scaled 2ahlength)
    ) cuttings;
    (q rotated 90 & reverse q rotated 270) shifted e
  enddef;

  vardef barTail expr p =
    save q,e; path q; pair e;
    e := point 0 of p;
    q := gobble(
      p shifted -e scaled 100
      cutbefore makepath(pencircle scaled 2ahlength)
    ) cuttings;
    (reverse q rotated 90 & q rotated 270) shifted e
  enddef;
  
  % The "drawarrow" implementation is based upon that in plain.mp (see above)
  %
  def addArrow
    (expr fRow, fCol, tRow, tCol)
    (expr fromTails, toHeads)
    (expr aLabel)
    (suffix position)
    (expr bend) =
    %
    save aTail, aHead, outAngle, anArrow;
    numeric aTail, aHead, outAngle;
    path anArrow;
    %
    outAngle := angle (w[tRow][tCol] - w[fRow][fCol]);
    anArrow := w[fRow][fCol]{dir (outAngle+bend)} .. {dir (outAngle-bend)}w[tRow][tCol];
    anArrow := anArrow cutbefore objLabel[fRow][fCol] enlarged 0.15cm ;
    anArrow := anArrow cutafter  objLabel[tRow][tCol] enlarged 0.15cm ;
    %
    % a local "drawarrow" implementation
    %
    draw anArrow ; 
    if 0 < length(fromTails) :
      for i = 1 upto length(fromTails) :
        aTail := ASCII (substring(i-1,i) of fromTails);
        if aTail = 124 : % "|"
          draw barTail anArrow ;
        elseif aTail = 111 : % "o"
          filldraw dotTail anArrow ;
        elseif aTail = 62 : % ">"
          filldraw firstArrowTail anArrow ;
        elseif aTail = 60 : % "<"
          filldraw firstReversedArrowTail anArrow ;
        elseif aTail = 93 : % "]"
          filldraw secondArrowTail anArrow ;
        elseif aTail = 91 : % "["
          filldraw secondReversedArrowTail anArrow ;
        fi ;
      endfor ;
    fi ;
    %
    if 0 < length(toHeads) :
      for i = 1 upto length(toHeads) :
        aHead := ASCII (substring(i-1,i) of toHeads);
        if aHead = 124 : % "|" 
          draw barHead anArrow ;
        elseif aHead = 111 : % "o"
          filldraw dotHead anArrow ;
        elseif aHead = 62 : % ">"
          filldraw firstArrowHead anArrow ;
        elseif aHead = 60 : % "<"
          filldraw firstReversedArrowHead anArrow ;
        elseif aHead = 93 : % "]"
          filldraw secondArrowHead anArrow ;
        elseif aHead = 91 : % "["
          filldraw secondReversedArrowHead anArrow ;
        fi ;
      endfor ;
    fi ;
    %
    if 0 < length(aLabel) :
      label position (
        thelabel("$"&aLabel&"$", origin ) ,
        point 0.5 of anArrow
      ) ;
    fi;
  enddef;
\stopMPdefinitions

\stopMkIVCode

\startMpIVCode

message("Loaded Commutative Diagram MetaPost definitions") ;

\stopMpIVCode

\stopchapter